plugins {
	id 'org.hidetake.ssh' version "2.9.0"
	id "java"
	id "eclipse"
	id "idea"
	id "jaci.openrio.gradle.GradleRIO" version "2017.1.1"
}

group 'org.usfirst.frc.team449.robot2017'
version '1.0'

allprojects {
	tasks.withType(Javadoc).all { enabled = false }
	repositories {
		maven {
			url 'https://jitpack.io'
		}
	}
}

compileJava {
	options.compilerArgs << '-parameters'
}

remotes {
	rio {
		host = 'roboRIO-449-frc.local'
		user = 'lvuser'
		knownHosts = allowAnyHosts
	}
}

repositories {
	mavenCentral()
	maven {
		name = "GradleRio"
		url = "http://dev.imjac.in/maven"
	}

	maven { url "https://plugins.gradle.org/m2/" }
}

dependencies {
	compile group: 'edu.wpi.first.wpilibj', name: 'wpilibJavaFinal', version: '2016.5.1'
	compile group: 'edu.wpi.first.wpilib.networktables.java', name: 'NetworkTables', version: '3.0.0'
	// https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.3'
	// https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.3'
	// https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-annotations
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.8.3'
	compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.8.3'
	//com.fasterxml.jackson.module:jackson-module-parameter-names:2.9.0.pr3
	compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names', version: '2.8.3'
	compile 'com.github.blair-robot-project:449-central-repo:v5.0.1-YAML'
	compile wpilib()
	compile talonSrx()
	compile fileTree(dir: "lib", include: "*.jar")
}

task convertFiles {
	doLast {
		fileTree("./src/main/resources").matching { include "*" }.each { aFile ->
			exec {
				commandLine 'dos2unix'
				args aFile.absolutePath
			}
		}
	}
}

frc {
	team = '449'
	robotClass = "org.usfirst.frc.team449.robot.Robot"
}

wpi {
	wpilibVersion = "+"
	// The WPILib version to use. For this version of GradleRIO, must be a 2017 version
	ntcoreVersion = "+"                   // The NetworkTables Core version to use.
	opencvVersion = "+"                   // The OpenCV version to use
	cscoreVersion = "+"                   // The CSCore version to use

	talonSrxVersion = "+"                 // The CTRE Toolsuite (Talon SRX) version to use.
}

def robotManifest = {
	attributes 'Main-Class': 'edu.wpi.first.wpilibj.RobotBase'
	attributes 'Robot-Class': frc.robotClass
}

jar {
	from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
	manifest robotManifest
}

task genJavadoc(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

task('copyResources') {
	doLast {
		ssh.run {
			session(remotes.rio) {
				execute 'mkdir ~/449_resources', ignoreError: true
				execute 'mkdir ~/logs', ignoreError: true
				put from: fileTree("./src/main/resources"), into: './449_resources/'
			}
		}
	}
}

artifacts {
	archives genJavadoc
}
/*

 */